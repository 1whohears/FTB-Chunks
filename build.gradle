buildscript {
	repositories {
		jcenter()
		maven { url = 'https://files.minecraftforge.net/maven' }
		maven { url "https://plugins.gradle.org/m2/" }
		maven { url "https://dist.creeper.host/Sponge/maven" }
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
		classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.+'
		classpath 'com.wynprice.cursemaven:CurseMaven:1.2.+'
		classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
	}
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'maven'
apply plugin: 'com.wynprice.cursemaven'
apply plugin: 'org.spongepowered.mixin'

apply from: 'https://raw.githubusercontent.com/MinecraftModDevelopment/Gradle-Collection/master/generic/markdown-git-changelog.gradle'

def ENV = System.getenv()
version = ENV.BUILD_NUMBER ? "${mod_version}.${ENV.BUILD_NUMBER}" : "${mod_version}.custom"
group = project.mod_package
archivesBaseName = project.mod_name

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
	mappings channel: 'snapshot', version: project.mapping_version
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
	runs {
		client {
			workingDirectory project.file('run')
			property 'forge.logging.console.level', 'debug'
			args '--mixin.config', 'ftbchunks.mixins.json'

			if (project.hasProperty('mc_dev_uuid')) args '--uuid', project.mc_dev_uuid
			if (project.hasProperty('mc_dev_username')) args '--username', project.mc_dev_username
			if (project.hasProperty('mc_dev_width')) args '--width', project.mc_dev_width
			if (project.hasProperty('mc_dev_height')) args '--height', project.mc_dev_height

			mods {
				ftbchunks {
					source sourceSets.main
				}
			}
		}

		server {
			workingDirectory project.file('run')
			property 'forge.logging.console.level', 'debug'
			args '--mixin.config', 'ftbchunks.mixins.json'

			mods {
				ftbchunks {
					source sourceSets.main
				}
			}
		}

		data {
			workingDirectory project.file('run')
			property 'forge.logging.console.level', 'debug'
			args '--mixin.config', 'ftbchunks.mixins.json'

			args '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/')

			mods {
				ftbchunks {
					source sourceSets.main
				}
			}
		}
	}
}

configurations.all {
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
	mavenLocal()
	maven { url "https://maven.tterrag.com/" } //Yarn2MCP
	maven { url "https://maven.latmod.com/" } //FTB GUI Library, FTB Teams, FTB Ranks, KubeJS
}

dependencies {
	minecraft "net.minecraftforge:forge:${forge_version}"
	compile(fg.deobf(project.dependencies.create("com.feed_the_beast.mods:ftb-gui-library:${ftb_gui_library_version}") {
		transitive = false
		changing = true
	}))
	compile(fg.deobf(project.dependencies.create("com.feed_the_beast.mods:ftb-teams:${ftb_teams_version}") {
		transitive = false
		changing = true
	}))
	compile(fg.deobf(project.dependencies.create("com.feed_the_beast.mods:ftb-ranks:${ftb_ranks_version}") {
		transitive = false
		changing = true
	}))
	//compileOnly(fg.deobf(project.dependencies.create("dev.latvian.mods:kubejs:${kubejs_version}") {
	//	transitive = false
	//	changing = true
	//}))
	//runtimeOnly(fg.deobf(project.dependencies.create("com.sunekaer.mods:YAMDA:${yamda_version}") {
	//	transitive = false
	//	changing = true
	//}))
	runtimeOnly fg.deobf(curse.resolve('biomes-o-plenty', project.bop_file))
}

jar {
	manifest {
		attributes([
				"MixinConfigs"            : "ftbchunks.mixins.json",
				"Specification-Title"     : project.mod_id,
				"Specification-Vendor"    : project.mod_author,
				"Specification-Version"   : "1",
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : version,
				"Implementation-Vendor"   : project.mod_author,
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
	type 'jar'
	builtBy 'reobfJar'
}

if (ENV.LOCAL_MAVEN) {
	uploadArchives {
		repositories {
			mavenDeployer {
				repository(url: "file://${ENV.LOCAL_MAVEN}")
			}
		}
	}
}

if (ENV.CURSEFORGE_KEY) {
	curseforge {
		apiKey = ENV.CURSEFORGE_KEY
		project {
			id = project.curseforge_id
			releaseType = "release"
			addGameVersion "1.16.2"
			relations {
				requiredDependency 'ftb-gui-library'
				optionalDependency 'ftb-ranks'
				optionalDependency 'ftb-teams'
				optionalDependency 'kubejs'
			}
			changelog = getGitChangelog
			changelogType = 'markdown'
		}
	}
}

mixin {
	add sourceSets.main, "ftbchunks.refmap.json"
}