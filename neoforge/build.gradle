plugins {
	id "com.github.johnrengelman.shadow" version "7.0.0"
	id "com.matthewprenger.cursegradle" version "1.4.0"
}

def ENV = System.getenv()

architectury {
	platformSetupLoomIde()
	neoForge()
}

configurations {
	common
	shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentNeoForge.extendsFrom common
	dev
}

loom {
	accessWidenerPath = project(":common").file("src/main/resources/ftbchunks.accesswidener")
}

artifacts {
	dev(jar)
}


repositories {
	maven {
		url "https://maven.neoforged.net/releases/"
	}
}

dependencies {
	neoForge "net.neoforged:neoforge:${rootProject.neoforge_version}"
	modApi "dev.architectury:architectury-neoforge:${rootProject.architectury_version}"

	modImplementation("dev.ftb.mods:ftb-library-neoforge:${rootProject.ftb_library_version}") { transitive = false }
	modImplementation("dev.ftb.mods:ftb-teams-neoforge:${rootProject.ftb_teams_version}") { transitive = false }

	common(project(path: ":common", configuration: "dev")) { transitive false }
	shadowCommon(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive false }
}

apply from: "https://raw.githubusercontent.com/FTBTeam/mods-meta/main/gradle/extra-local-mods.gradle"

processResources {
	def file = new File("./build/resources/main/META-INF/neoforge.mods.toml")
	if (file.exists()) {
		file.delete()
	}

	exclude '.cache'
	inputs.property "version", project.version

	filesMatching("META-INF/neoforge.mods.toml") {
		expand "version": project.version,
				"archversion": project.architectury_version,
				"neoforgeversion": project.neoforge_version,
				"neoforgeloaderversion": project.neoforge_loader_version,
				"mcversion": project.minecraft_version,
				"ftblibraryversion": project.ftb_library_version,
				"ftbteamsversion": project.ftb_teams_version
	}
}

shadowJar {
	exclude "fabric.mod.json"

	configurations = [project.configurations.shadowCommon]
	setArchiveClassifier("dev-shadow")
}

remapJar {
	input.set shadowJar.archiveFile
	dependsOn shadowJar
	archiveBaseName.set "${rootProject.archives_base_name}-${project.name}"
	archiveClassifier.set null
	atAccessWideners.add('ftbchunks.accesswidener')
}

jar {
	manifest {
		attributes([
				"Specification-Title"     : project.mod_id,
				"Specification-Vendor"    : project.mod_author,
				"Specification-Version"   : "1",
				"Implementation-Title"    : project.name,
				"Implementation-Version"  : version,
				"Implementation-Vendor"   : project.mod_author,
				"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}

	setArchiveClassifier("dev")
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

if (ENV.CURSEFORGE_KEY) {
	curseforge {
		apiKey = ENV.CURSEFORGE_KEY
		project {
			id = project.curseforge_id_forge
			releaseType = ftbPublishing.relType
			addGameVersion "NeoForge"
			addGameVersion project.minecraft_version
			mainArtifact(remapJar.archiveFile)
			relations {
				requiredDependency 'architectury-api'
				requiredDependency 'ftb-library-forge'
				requiredDependency 'ftb-teams-forge'
				optionalDependency 'ftb-ranks-forge'
			}
			changelog = ENV.CHANGELOG  // expected to exist if ENV.CURSEFORGE_KEY does
			changelogType = 'markdown'
		}
	}
}

publishing {
	publications {
		mavenForge(MavenPublication) {
			artifactId = "${rootProject.archives_base_name}-${project.name}"
			version ftbPublishing.mavenVersion
			from components.java
		}
	}

	repositories {
		if (ftbPublishing.ftbToken) {
			maven {
				url ftbPublishing.ftbURL
				credentials {
					username = ftbPublishing.ftbUser
					password = ftbPublishing.ftbToken
				}
			}
		}

		if (ftbPublishing.sapsToken) {
			maven {
				url ftbPublishing.sapsURL
				credentials {
					username = ftbPublishing.sapsUser
					password = ftbPublishing.sapsToken
				}
			}
		}
	}
}

rootProject.tasks.getByName("curseforgePublish").dependsOn tasks.getByName("curseforge")
